---
title: Quick Start
sidebar_label: Quick Start
---

This guide introduces the FoBE Breakout BM8563 and how to use it.

## Hardware diagram

The following figure illustrates the FoBE Breakout BM8563 hardware diagram.

<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.digram.png" alt="FoBE Breakout BM8563 Hardware Diagram" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

## Mechanical dimensions

FoBE Breakout BM8563 is a single-sided 25.4mm x 25.4mm (1" x 1") 1.6mm thick PCB with two SH1.0 4-pin connectors and a set of 4-pin 2.54mm header holes. Fixing by 6 x 1.6mm Screw holes.

<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.dimensions.png" alt="FoBE Breakout BM8563 Mechanical Dimensions" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

## Interfaces

The board provides two 4-pin SH1.0 connectors, all providing the same IIC connections for MCU control:

  |Interface                           |Description |
  |-------------------------------|---------|
  |3V3 |Power supply       |
  |GND  |Ground       |
  |SDA |I2C bus data line       |
  |SCL |I2C bus clock line       |

And a set of 6-pin 2.54mm header holes, providing the same IIC connections for MCU control, plus an interrupt pin and a programmable output pin.

  |Interface                           |Description |
  |-------------------------------|---------|
  |INT  |Trigger when the programmed timing conditions are met: Active Low      |
  |GND  |Ground       |
  |3V3 |Power supply       |
  |SDA |I2C bus data line       |
  |SCL |I2C bus clock line       |
  |OUT  |CLK_OUT: Can output programmable square wave: Active Low       |

## Configuration Jumper

The board features two jumper pad:

|Interface                           |Description |
|-------------------------------|---------|
|LED-JUMPER |The PPS (Pulse Per Second) LED jumper. To save power, you can physically cut the trace between the pads of this jumper. You can later re-solder the jumper to reconnect the PPS LED. |
|IIC PULL-UP |By default, sda/scl are pulled up, you can physically cut the trace between the pads of this jumper if you don't need them.|

## Programming

### Running with FoBE Quill ESP32S3 Mesh

Let's get started with the FoBE Quill ESP32S3 Mesh using the MFP interface.

1. Connect the FoBE Breakout BM8563 to the FoBE Quill ESP32S3 Mesh using the MFP interface.
<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.connection.png" alt="FoBE Breakout BM8563 Connection" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

2. Create a sketch or PlatformIO project, or follow the [FoBE Quill ESP32S3 Programming Guide](/product/f1102/programming) for pre-configuration.

3. Install the necessary library in your project:
```ini
makuna/RTC@^2.5.0
```

4. Copy the following code into your sketch or PlatformIO project:
```cpp
#include <RtcPCF8563.h>

#define I2C_SDA_PIN PIN_MFP3    // SDA pin for MFP to I2C cable, change if needed
#define I2C_SCL_PIN PIN_MFP4    // SCL pin for MFP to I2C cable, change if needed
#define PERI_EN_PIN PIN_PERI_EN // Peripheral enable pin, change if needed

RtcPCF8563<TwoWire> rtcSensor(Wire);

void setup()
{
  // Initialize serial communication
  Serial.begin(115200);
  Serial.println("Serial initialized");

  // Initialize peripheral power
  if (PERI_EN_PIN >= 0)
  {
    pinMode(PERI_EN_PIN, OUTPUT);
    digitalWrite(PERI_EN_PIN, HIGH); // Enable peripheral power
    Serial.println("Peripheral power enabled");
  }

  // Initialize I2C bus
  Wire.begin(I2C_SDA_PIN, I2C_SCL_PIN);
  rtcSensor.Begin();
  Serial.println("I2C bus initialized");

  // Initialize RTC sensor
#if defined(WIRE_HAS_TIMEOUT)
  Wire.setWireTimeout(3000 /* us */, true /* reset_on_timeout */);
#endif
  Serial.println("BM8563 RTC sensor initialized");

  // Configure RTC if needed
  RtcDateTime compiled = RtcDateTime(__DATE__, __TIME__);
  if (!rtcSensor.IsDateTimeValid() || rtcSensor.GetDateTime() < compiled)
  {
    rtcSensor.SetDateTime(compiled);
  }

  if (!rtcSensor.GetIsRunning())
  {
    rtcSensor.SetIsRunning(true);
  }

  // Clear alarms and timers
  rtcSensor.StopAlarm();
  rtcSensor.StopTimer();
  rtcSensor.SetSquareWavePin(PCF8563SquareWavePinMode_None);
}

void loop()
{
  delay(1000); // Wait 1 second between readings

  if (rtcSensor.IsDateTimeValid())
  {
    RtcDateTime now = rtcSensor.GetDateTime();

    // Display data on screen
    Serial.print("\033[H\033[J");
    Serial.println("> FoBE Breakout BM8563 RTC Monitor");
    Serial.println();
    Serial.print("\033[7m");
    Serial.printf("%-12s%-12s\n", "INDEX", "VALUE");
    Serial.print("\033[0m");
    Serial.printf("%-12s%02u/%02u/%04u\n", "Date", now.Month(), now.Day(), now.Year());
    Serial.printf("%-12s%02u:%02u:%02u\n", "Time", now.Hour(), now.Minute(), now.Second());
    Serial.printf("%-12s%-12s\n", "Status", rtcSensor.GetIsRunning() ? "Running" : "Stopped");
    Serial.printf("%-12s%-12s\n", "Valid", rtcSensor.IsDateTimeValid() ? "Yes" : "No");
  }
}
```

:::important
This example code uses ANSI output formatting. Your terminal must support ANSI escape codes to display the output correctly.

```ini
# platformio.ini
[env:fobe_quill_esp32s3_mesh]
platform = FoBE Espressif 32
board = fobe_quill_esp32s3_mesh
framework = arduino
lib_deps = 
	makuna/RTC@^2.5.0
monitor_speed = 115200
monitor_raw = true
```
:::

5. Build and upload the project. You should see the FoBE Breakout BM8563 Monitor output in the serial monitor (raw mode).

```bash
> FoBE Breakout BM8563 RTC Monitor

INDEX       VALUE       
Date        08/09/2025
Time        02:04:48
Status      Running     
Valid       Yes         
```