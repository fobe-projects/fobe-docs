---
title: Quick Start
sidebar_label: Quick Start
---

This guide introduces the FoBE Breakout BM8563 and how to use it.

## Hardware diagram

The following figure illustrates the FoBE Breakout BM8563 hardware diagram.

<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.digram.png" alt="FoBE Breakout BM8563 Hardware Diagram" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

## Mechanical dimensions

FoBE Breakout BM8563 is a single-sided 25.4mm x 25.4mm (1" x 1") 1.6mm thick PCB with two SH1.0 4-pin connectors and a set of 4-pin 2.54mm header holes. Fixing by 6 x 1.6mm Screw holes.

<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.dimensions.png" alt="FoBE Breakout BM8563 Mechanical Dimensions" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

## Interfaces

The board provides two 4-pin SH1.0 connectors, all providing the same IIC connections for MCU control:

  |Interface                           |Description |
  |-------------------------------|---------|
  |3V3 |Power supply       |
  |GND  |Ground       |
  |SDA |I2C bus data line       |
  |SCL |I2C bus clock line       |

And a set of 6-pin 2.54mm header holes, providing the same IIC connections for MCU control, plus an interrupt pin and a programmable output pin.

  |Interface                           |Description |
  |-------------------------------|---------|
  |INT  |Trigger when the programmed timing conditions are met: Active Low      |
  |GND  |Ground       |
  |3V3 |Power supply       |
  |SDA |I2C bus data line       |
  |SCL |I2C bus clock line       |
  |OUT  |CLK_OUT: Can output programmable square wave: Active Low       |

## Configuration Jumper

The board features two jumper pad:

|Interface                           |Description |
|-------------------------------|---------|
|LED-JUMPER |The PPS (Pulse Per Second) LED jumper. To save power, you can physically cut the trace between the pads of this jumper. You can later re-solder the jumper to reconnect the PPS LED. |
|IIC PULL-UP |By default, sda/scl are pulled up, you can physically cut the trace between the pads of this jumper if you don't need them.|

## Programming

### FoBE Quill ESP32-S3 Mesh Example
The FoBE Quill ESP32-S3 Mesh development board provides multiple digital gpio; 

MFP interface is used in the example:
<br/>
<div style={{ textAlign: 'center' }}>
  <img src="/img/products/f2204-r1a.connection.png" alt="FoBE Breakout BM8563 Mechanical connection" style={{ width: '80%', height: 'auto' }} />
</div>
<br/>

Please follow the [FoBE Quill ESP32-S3 Programming Guide](/product/f1102/programming) for pre-configuration.

And the necessary Library Dependency:
> makuna/RTC@^2.5.0

```C++
#include <Arduino.h>
#include <RtcPCF8563.h>
#include <Wire.h>

RtcPCF8563<TwoWire> Rtc(Wire);

#define IIC_SDA_PIN PIN_MFP3
#define IIC_SCL_PIN PIN_MFP4

bool wasError(const char *errorTopic = "") {
  uint8_t error = Rtc.LastError();
  if (error != 0) {
    // we have a communications error
    // see
    // https://www.arduino.cc/reference/en/language/functions/communication/wire/endtransmission/
    // for what the number means
    Serial.print("[");
    Serial.print(errorTopic);
    Serial.print("] WIRE communications error (");
    Serial.print(error);
    Serial.print(") : ");

    switch (error) {
    case Rtc_Wire_Error_None:
      Serial.println("(none?!)");
      break;
    case Rtc_Wire_Error_TxBufferOverflow:
      Serial.println("transmit buffer overflow");
      break;
    case Rtc_Wire_Error_NoAddressableDevice:
      Serial.println("no device responded");
      break;
    case Rtc_Wire_Error_UnsupportedRequest:
      Serial.println("device doesn't support request");
      break;
    case Rtc_Wire_Error_Unspecific:
      Serial.println("unspecified error");
      break;
    case Rtc_Wire_Error_CommunicationTimeout:
      Serial.println("communications timed out");
      break;
    }
    return true;
  }
  return false;
}

void printDateTime(const RtcDateTime &dt) {
  char datestring[26];

  snprintf_P(datestring, countof(datestring),
             PSTR("%02u/%02u/%04u %02u:%02u:%02u"), dt.Month(), dt.Day(),
             dt.Year(), dt.Hour(), dt.Minute(), dt.Second());
  Serial.print(datestring);
}

void setup() {
  Serial.begin(115200);

  Serial.print("compiled: ");
  Serial.print(__DATE__);
  Serial.println(__TIME__);

  pinMode(PIN_PERI_EN, OUTPUT);
  digitalWrite(PIN_PERI_EN, HIGH);   // Enable peripheral power

  Wire.begin(IIC_SDA_PIN, IIC_SCL_PIN);  

  Rtc.Begin();
#if defined(WIRE_HAS_TIMEOUT)
  Wire.setWireTimeout(3000 /* us */, true /* reset_on_timeout */);
#endif

  RtcDateTime compiled = RtcDateTime(__DATE__, __TIME__);
  printDateTime(compiled);
  Serial.println();

  if (!Rtc.IsDateTimeValid()) {
    if (!wasError("setup IsDateTimeValid")) {
      // Common Causes:
      //    1) first time you ran and the device wasn't running yet
      //    2) the battery on the device is low or even missing

      Serial.println("RTC lost confidence in the DateTime!");

      // following line sets the RTC to the date & time this sketch was compiled
      // it will also reset the valid flag internally unless the Rtc device is
      // having an issue

      Rtc.SetDateTime(compiled);
    }
  }

  if (!Rtc.GetIsRunning()) {
    if (!wasError("setup GetIsRunning")) {
      Serial.println("RTC was not actively running, starting now");
      Rtc.SetIsRunning(true);
    }
  }

  RtcDateTime now = Rtc.GetDateTime();
  if (!wasError("setup GetDateTime")) {
    if (now < compiled) {
      Serial.println("RTC is older than compile time, updating DateTime");
      Rtc.SetDateTime(compiled);
    } else if (now > compiled) {
      Serial.println("RTC is newer than compile time, this is expected");
    } else if (now == compiled) {
      Serial.println("RTC is the same as compile time, while not expected all "
                     "is still fine");
    }
  }

  // never assume the Rtc was last configured by you, so
  // just clear them to your needed state
  Rtc.StopAlarm();
  wasError("setup StopAlarm");
  Rtc.StopTimer();
  wasError("setup StopTimer");
  Rtc.SetSquareWavePin(PCF8563SquareWavePinMode_None);
  wasError("setup SetSquareWavePin");
}

void loop() {
  if (!Rtc.IsDateTimeValid()) {
    if (!wasError("loop IsDateTimeValid")) {
      // Common Causes:
      //    1) the battery on the device is low or even missing and the power
      //    line was disconnected
      Serial.println("RTC lost confidence in the DateTime!");
    }
  }

  RtcDateTime now = Rtc.GetDateTime();
  if (!wasError("loop GetDateTime")) {
    printDateTime(now);
    Serial.println();
  }

  delay(5000); // five seconds
}

#define countof(a) (sizeof(a) / sizeof(a[0]))

```

Build and flash the project, you should see the current date and time printed in the serial monitor.

> - If you want to use custom PINS, you can change the `IIC_SDA_PIN` and `IIC_SCL_PIN` definitions to your desired GPIO pins.